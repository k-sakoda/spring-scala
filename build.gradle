buildscript {
    repositories { mavenCentral() }
    dependencies { classpath 'org.ajoberstar:gradle-git:1.3.0' }
}

plugins {
    id 'scala'
    id 'war'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '1.2.1'
}

defaultTasks 'clean','jar','sourcesJar','shadowJar','war'

sourceCompatibility = 1.6
version = '0.20151125'

repositories.mavenCentral()

import org.ajoberstar.grgit.*

sourceSets {
    examples {
        resources.srcDir 'src/examples/resources'
        scala {
            srcDir 'src/examples/scala'
        }
        compileClasspath += sourceSets.main.runtimeClasspath
    }
    //test.runtimeClasspath += examples.runtimeClasspath
    test.resources.srcDirs += examples.resources.srcDirs
}

ext {
    springVersion = "4.2.3.RELEASE"
    jacksonVersion = "2.6.3"
}

idea {
    module {
        //and some extra test source dirs
        scopes.TEST.plus += [ configurations.examplesCompile ]
    }
}

jar {
    manifest {
        attributes 'Git-Commit-Id': Grgit.open(project.file('.')).head().id
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

shadowJar {
    manifest {
        attributes 'Main-Class': 'com.walbrix.spring.REPL'
    }
    configurations = [project.configurations.runtime, project.configurations.testRuntime]
    mergeServiceFiles {
        include 'META-INF/spring.*'
    }
}

task push(type: Exec, dependsOn: [jar,sourcesJar]) {
    commandLine './push.sh',version
}

configurations {
    all*.exclude module: 'commons-logging'
    examplesCompile
    examplesCompile.extendsFrom compile
}

war {
    //into('src') { from 'src' }
    //into('') { from 'build.gradle','karma.conf.js','spring-scala.pom','push.sh','*.md' }
    from('.') {
        include "src/**/*"
        exclude "**/*.xz"
        include "build.gradle","karma.conf.js","spring-scala.pom","push.sh","*.md"
    }
    webAppDirName = 'src/examples/webapp'
    classpath += sourceSets.examples.output
    manifest {
        attributes("Implementation-Title": project.name, "Implementation-Version": version, "Implementation-Timestamp": new Date(), 'Git-Commit-Id': Grgit.open(project.file('.')).head().id)
    }
}

dependencies {
//    testRuntime sourceSets.examples.output
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'org.springframework:spring-test:' + springVersion

    testCompile 'org.eclipse.jetty:jetty-plus:9.2.14.v20151106'
    testCompile 'org.scala-lang:scala-compiler:2.11.7'
    testCompile 'jline:jline:2.13'
    testCompile 'org.seleniumhq.selenium:selenium-chrome-driver:2.48.2'
    testCompile 'org.scalatest:scalatest_2.11:2.2.5'
    testRuntime 'org.eclipse.jetty:jetty-jsp:9.2.14.v20151106'
    testRuntime 'org.seleniumhq.selenium:selenium-support:2.48.2'

    runtime 'org.slf4j:jcl-over-slf4j:1.7.12'
    runtime 'org.aspectj:aspectjweaver:1.8.7'
    runtime 'javax.servlet:jstl:1.2'
    runtime 'commons-fileupload:commons-fileupload:1.3.1'

    compile 'org.scala-lang:scala-library:2.11.7'
    compile 'org.scala-lang.modules:scala-xml_2.11:1.0.5'
    compile 'com.typesafe.scala-logging:scala-logging-slf4j_2.11:2.1.2'
    compile 'ch.qos.logback:logback-classic:1.1.3'
    compile 'com.github.scopt:scopt_2.11:3.3.0'
    compile 'org.springframework:spring-webmvc:' + springVersion
    compile 'org.springframework:spring-jdbc:' + springVersion
    compile 'org.springframework:spring-context-support:' + springVersion
    compile 'org.scalikejdbc:scalikejdbc_2.11:2.2.9'
    compile 'org.flywaydb:flyway-core:3.2.1'
    compile 'org.apache.velocity:velocity:1.7'
    compile 'commons-io:commons-io:2.4'
    compile 'commons-codec:commons-codec:1.10'
    compile 'com.yahoo.platform.yui:yuicompressor:2.4.8'
    compile 'com.fasterxml.jackson.core:jackson-databind:' + jacksonVersion
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-joda:' + jacksonVersion
    compile 'com.fasterxml.jackson.module:jackson-module-scala_2.11:' + jacksonVersion
    compile 'org.pegdown:pegdown:1.5.0'
    compile 'org.json4s:json4s-native_2.11:3.3.0'
    compile 'org.json4s:json4s-jackson_2.11:3.3.0'
    compile 'org.json4s:json4s-ext_2.11:3.3.0'

    // deps for examples
    examplesCompile sourceSets.main.output
    compile 'com.h2database:h2:1.4.190'
    compile 'mysql:mysql-connector-java:5.1.37'
    compile 'net.sf.opencsv:opencsv:2.3'
    compile 'org.tukaani:xz:1.5'
    compile 'org.apache.httpcomponents:httpclient:4.5.1'
    compile 'org.jsoup:jsoup:1.8.3'
    compile 'com.rometools:rome:1.5.1'
    compile 'org.twitter4j:twitter4j-core:4.0.4'
    compile 'org.bouncycastle:bcprov-jdk16:1.46'
    compile 'com.nimbusds:nimbus-jose-jwt:4.1.1'
    compile 'org.scalatra:scalatra-json_2.11:2.3.1'

    // メールセッションはアプリケーションサーバ任せにするのでこのdepはwarに入れない
    providedCompile 'javax.mail:mail:1.4.7'
    // Servlet APIはアプリケーションサーバの classpathにあるものを使うのでこのdepはwarに入れない
    providedCompile 'org.eclipse.jetty:jetty-webapp:9.2.14.v20151106'

}
